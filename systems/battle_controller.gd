extends Node3D
class_name BattleController

signal move_mode_toggled(is_active: bool)
signal attack_mode_toggled(is_active: bool)
signal attack_preview_changed(text: String)
signal attack_resolved(attacker: TacticalUnit, target: TacticalUnit, did_hit: bool, hit_chance: int)
signal action_state_changed(is_busy: bool)
signal units_registered(player_units: Array[TacticalUnit])
signal debug_enemy_control_changed(enabled: bool)
signal debug_player_ai_changed(enabled: bool)
signal ai_decision_recorded(record: Dictionary)

@export var tactical_unit: TacticalUnit
@export var mouse_raycaster: MouseRaycaster
@export var grid_cursor: GridCursor
@export var path_visualizer: PathVisualizer
@export var cover_visualizer: CoverVisualizer
@export var shot_trajectory_visualizer: ShotTrajectoryVisualizer
@export var grid_manager: GridManager
@export var turn_manager: TurnManager
@export var debug_shots: bool = false

const UNIFORM_AP_COST = 1

var pathfinder := Pathfinder.new()
var last_map_validation: MapValidationResult
var current_movement_zone: Array[Vector3i] = []
var current_attack_zone: Array[Vector3i] = []
var _last_attack_preview := ""
var debug_enemy_control: bool = false
var debug_player_ai: bool = false
var is_action_in_progress: bool = false:
	set(value):
		if is_action_in_progress != value:
			is_action_in_progress = value
			action_state_changed.emit(value)
var is_move_mode_active: bool = false:
	set(value):
		if is_move_mode_active != value:
			is_move_mode_active = value
			move_mode_toggled.emit(is_move_mode_active)
			if not is_move_mode_active:
				path_visualizer.clear_path()
				path_visualizer.clear_range_zone()
				cover_visualizer.clear()

var is_attack_mode_active: bool = false:
	set(value):
		if is_attack_mode_active != value:
			is_attack_mode_active = value
			attack_mode_toggled.emit(is_attack_mode_active)
			if not is_attack_mode_active:
				path_visualizer.clear_range_zone()
				if shot_trajectory_visualizer:
					shot_trajectory_visualizer.clear()

func _ready() -> void:
	if not mouse_raycaster or not grid_manager or not grid_manager.map_floor or not path_visualizer or not cover_visualizer or not turn_manager or not grid_cursor:
		push_error("Missing critical node assignments on BattleController!")
		return

	turn_manager.turn_phase_changed.connect(_on_turn_phase_changed)
	turn_manager.active_unit_changed.connect(_on_active_unit_changed)
	mouse_raycaster.floor_clicked.connect(_on_floor_clicked)
	mouse_raycaster.unit_clicked.connect(_on_unit_clicked)

func initialize_battle(prebuilt_map: MapData = null) -> bool:
	if prebuilt_map:
		grid_manager.map_data = prebuilt_map
		MapGraphBuilder.build(prebuilt_map, pathfinder)
	else:
		pathfinder.clear()
		MapBuilder.build(grid_manager, pathfinder)
		# Let CSG collision bodies enter the physics world before scanning.
		await get_tree().create_timer(0.05).timeout
		MapBuilder.scan_obstacles(get_world_3d(), grid_manager, pathfinder)
	last_map_validation = MapValidator.validate(grid_manager.map_data, pathfinder, {
		TacticalUnit.Faction.PLAYER: turn_manager.player_units.size(),
		TacticalUnit.Faction.ENEMY: turn_manager.enemy_units.size(),
	})
	if not last_map_validation.is_valid():
		push_error("Battlefield validation failed:\n%s" % last_map_validation.describe())
		return false
	print_rich("[color=green][MapValidator][/color] PASSED — %d cells, %d traversal links, %d spawn cells" % [
		grid_manager.map_data.cells.size(),
		grid_manager.map_data.traversal_links.size(),
		grid_manager.map_data.get_total_spawn_count(),
	])

	for unit_item in turn_manager.player_units + turn_manager.enemy_units:
		var start_grid = world_to_grid(unit_item.global_position - Vector3.UP * unit_item.standing_height)
		grid_manager.register_unit(unit_item, start_grid)
		unit_item.defeated.connect(_on_unit_defeated)
	units_registered.emit(turn_manager.player_units)

	turn_manager.start_battle()
	return true

func toggle_move_mode() -> void:
	if is_current_phase_manually_controlled() and not is_action_in_progress:
		is_move_mode_active = not is_move_mode_active
		if is_move_mode_active:
			is_attack_mode_active = false
			update_unit_movement_zone()

func toggle_attack_mode() -> void:
	if not is_current_phase_manually_controlled() or is_action_in_progress:
		return
	if not tactical_unit or not tactical_unit.stats or tactical_unit.stats.current_ap < UNIFORM_AP_COST:
		return

	if is_attack_mode_active:
		is_attack_mode_active = false
		return

	if is_move_mode_active:
		is_move_mode_active = false
	is_attack_mode_active = true
	update_attack_range()

func _process(_delta: float) -> void:
	if not turn_manager or not is_current_phase_manually_controlled():
		return

	var floor_hit = mouse_raycaster.get_floor_raycast_result() if mouse_raycaster else {}
	if not floor_hit.is_empty() and grid_cursor:
		grid_cursor.update_hover_position(floor_hit.position)
	if not is_move_mode_active or not is_instance_valid(tactical_unit) or tactical_unit.is_moving:
		path_visualizer.clear_path()
	else:
		_update_movement_preview(floor_hit)

	_update_attack_preview()

func _update_movement_preview(floor_hit: Dictionary) -> void:
	if not floor_hit.is_empty():
		var hover_grid = world_to_grid(floor_hit.position)
		if current_movement_zone.has(hover_grid):
			var path = _get_path_to_position(floor_hit.position)
			if path.size() > 1:
				path_visualizer.draw_path(path, Color(0.0, 0.5, 1.0, 0.4))
				return
	path_visualizer.clear_path()

func _update_attack_preview() -> void:
	var preview = ""
	var trajectory_drawn := false
	if is_attack_mode_active and is_instance_valid(tactical_unit):
		var hovered = mouse_raycaster.get_unit_under_mouse()
		if is_instance_valid(hovered) and hovered != tactical_unit:
			var evaluation = evaluate_attack(tactical_unit, hovered)
			preview = "%d%% HIT" % evaluation.hit_chance if evaluation.is_legal else evaluation.reason.to_upper()
			if shot_trajectory_visualizer:
				shot_trajectory_visualizer.draw_trajectory(
					CombatRules.get_shot_origin(tactical_unit, grid_manager),
					CombatRules.get_shot_destination(hovered, grid_manager),
					evaluation
				)
				trajectory_drawn = true
	if not trajectory_drawn and shot_trajectory_visualizer:
		shot_trajectory_visualizer.clear()
	if preview != _last_attack_preview:
		_last_attack_preview = preview
		attack_preview_changed.emit(preview)

func _on_unit_clicked(unit: TacticalUnit) -> void:
	if is_action_in_progress or not is_instance_valid(unit) or not unit.stats or unit.stats.is_defeated:
		return
	if is_attack_mode_active and is_instance_valid(tactical_unit) \
		and FactionRules.are_hostile(tactical_unit.faction, unit.faction):
		try_attack(tactical_unit, unit)
		return

	if not debug_player_ai and turn_manager.select_player_unit(unit):
		is_move_mode_active = false
		is_attack_mode_active = false

func _on_floor_clicked(raw_position: Vector3) -> void:
	if is_action_in_progress or not is_move_mode_active or not is_instance_valid(tactical_unit):
		return

	var clicked_grid = world_to_grid(raw_position)
	if not current_movement_zone.has(clicked_grid):
		return

	var path = _get_path_to_position(raw_position)
	if path.is_empty():
		return

	await try_move(tactical_unit, clicked_grid)

func _on_turn_phase_changed(new_phase: TurnManager.TurnPhase) -> void:
	var is_player_control = is_current_phase_manually_controlled()
	grid_cursor.visible = is_player_control
	if not is_player_control:
		is_move_mode_active = false
		is_attack_mode_active = false

func _on_active_unit_changed(unit: TacticalUnit) -> void:
	tactical_unit = unit
	is_move_mode_active = false
	is_attack_mode_active = false

func update_unit_movement_zone() -> void:
	if not tactical_unit or tactical_unit.is_moving:
		return

	var movement_budget = tactical_unit.stats.speed if tactical_unit.stats else 6
	var unit_grid = grid_manager.get_unit_grid(tactical_unit)
	var raw_reachable = pathfinder.get_reachable_cells(unit_grid, movement_budget)

	current_movement_zone = raw_reachable.filter(
		func(cell: Vector3i) -> bool:
			if cell == unit_grid:
				return true
			return grid_manager.can_unit_occupy_cell(tactical_unit, cell)
	)

	path_visualizer.draw_range_zone(current_movement_zone, Color(0.9, 0.8, 0.1, 0.25))
	cover_visualizer.draw_for_cells(current_movement_zone)

func update_attack_range() -> void:
	current_attack_zone.clear()
	if not tactical_unit or not tactical_unit.stats:
		path_visualizer.clear_range_zone()
		return

	var attacker_grid = grid_manager.get_unit_grid(tactical_unit)
	for grid_pos in pathfinder.grid_to_id_map.keys():
		var point_id = pathfinder.grid_to_id_map[grid_pos]
		if pathfinder.astar.is_point_disabled(point_id):
			continue

		var distance = absi(attacker_grid.x - grid_pos.x) + absi(attacker_grid.y - grid_pos.y) + absi(attacker_grid.z - grid_pos.z)
		if distance == 0 or distance > tactical_unit.attack_range:
			continue

		var world_pos = grid_manager.grid_to_world(grid_pos)
		if CombatRules.has_line_of_sight_to_position(tactical_unit, world_pos, grid_manager, get_world_3d()):
			current_attack_zone.append(grid_pos)

	path_visualizer.draw_range_zone(current_attack_zone, Color(0.95, 0.2, 0.2, 0.3))

func _get_path_to_position(target_world_pos: Vector3) -> PackedVector3Array:
	var start_grid = grid_manager.get_unit_grid(tactical_unit)
	var end_grid = world_to_grid(target_world_pos)
	return pathfinder.calculate_3d_path(start_grid, end_grid)

func world_to_grid(pos: Vector3) -> Vector3i:
	return grid_manager.world_to_grid(pos)

func can_attack(attacker: TacticalUnit, target: TacticalUnit) -> bool:
	return evaluate_attack(attacker, target).is_legal

func evaluate_attack(attacker: TacticalUnit, target: TacticalUnit) -> CombatRules.AttackEvaluation:
	return CombatRules.evaluate_attack(attacker, target, grid_manager, get_world_3d())

func set_debug_enemy_control(enabled: bool) -> void:
	if debug_enemy_control == enabled:
		return
	debug_enemy_control = enabled
	is_move_mode_active = false
	is_attack_mode_active = false
	debug_enemy_control_changed.emit(enabled)
	_on_turn_phase_changed(turn_manager.current_phase)

func set_debug_player_ai(enabled: bool) -> void:
	if debug_player_ai == enabled:
		return
	debug_player_ai = enabled
	is_move_mode_active = false
	is_attack_mode_active = false
	debug_player_ai_changed.emit(enabled)
	_on_turn_phase_changed(turn_manager.current_phase)

func is_current_phase_manually_controlled() -> bool:
	if not turn_manager:
		return false
	return (not debug_player_ai and turn_manager.current_phase == TurnManager.TurnPhase.PLAYER_TURN) \
		or (debug_enemy_control and turn_manager.current_phase == TurnManager.TurnPhase.ENEMY_TURN)

func record_ai_decision(actor: TacticalUnit, action: String, subject: String, reason: String, alternatives: String) -> void:
	ai_decision_recorded.emit({
		"actor": actor.name if is_instance_valid(actor) else "Unknown",
		"action": action,
		"subject": subject,
		"reason": reason,
		"alternatives": alternatives,
	})

func try_attack(attacker: TacticalUnit, target: TacticalUnit) -> bool:
	if is_action_in_progress or not turn_manager.can_unit_act(attacker):
		return false
	var evaluation = evaluate_attack(attacker, target)
	if debug_shots:
		var blocker = CombatRules.get_blocking_cell(attacker.global_position, target.global_position, grid_manager)
		print("[Shot] ", attacker.name, " ", grid_manager.get_unit_grid(attacker), " -> ", target.name, " ", grid_manager.get_unit_grid(target), " legal=", evaluation.is_legal, " chance=", evaluation.hit_chance, " reason=", evaluation.reason)
		if blocker:
			print("[Shot] blocker=", blocker.grid_position, " cover=", blocker.cover_type, " height=", blocker.cover_height, " walkable=", blocker.walkable)
	if not evaluation.is_legal:
		return false

	var attack_cmd = AttackAction.new(attacker, target, UNIFORM_AP_COST, evaluation.hit_chance)
	if not attack_cmd.execute():
		return false
	attack_resolved.emit(attacker, target, attack_cmd.did_hit, evaluation.hit_chance)

	is_attack_mode_active = false
	is_move_mode_active = false
	return true

func try_defend(unit: TacticalUnit) -> bool:
	if is_action_in_progress or not turn_manager.can_unit_act(unit):
		return false
	var action = DefendAction.new(unit, UNIFORM_AP_COST)
	if not action.execute():
		return false
	is_move_mode_active = false
	is_attack_mode_active = false
	return true

func try_move(unit: TacticalUnit, target_cell: Vector3i) -> bool:
	if is_action_in_progress or not turn_manager.can_unit_act(unit):
		return false
	var movement_budget = unit.stats.speed if unit.stats else 0
	var start_cell = grid_manager.get_unit_grid(unit)
	if not pathfinder.get_reachable_cells(start_cell, movement_budget).has(target_cell):
		return false
	var path = pathfinder.calculate_3d_path(start_cell, target_cell)
	if path.is_empty():
		return false
	var action = MoveAction.new(unit, target_cell, _build_movement_path(unit, path), grid_manager, UNIFORM_AP_COST)
	if not action.execute():
		return false
	is_action_in_progress = true
	is_move_mode_active = false
	is_attack_mode_active = false
	await unit.movement_finished
	is_action_in_progress = false
	return true

func _build_movement_path(unit: TacticalUnit, path: PackedVector3Array) -> PackedVector3Array:
	var animated_path := PackedVector3Array()
	for point in path:
		var cell = grid_manager.get_cell_data(world_to_grid(point))
		var standing_height = unit.standing_height
		if cell and cell.cover_type == MapCellData.CoverType.LOW:
			standing_height += cell.cover_height
		animated_path.append(point + Vector3.UP * standing_height)
	return animated_path

func _on_unit_defeated(unit: TacticalUnit) -> void:
	if not is_instance_valid(unit):
		return

	grid_manager.unregister_unit_at(grid_manager.get_unit_grid(unit))
	turn_manager.remove_unit(unit)
	if tactical_unit == unit:
		tactical_unit = null
	is_move_mode_active = false
	is_attack_mode_active = false
	unit.queue_free()
